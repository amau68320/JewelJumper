#version 430

layout(local_size_x = 16, local_size_y = 16, local_size_z = 2) in;
layout(r32ui, binding = 0) uniform readonly  uimage3D u_Src;
layout(r32ui, binding = 1) uniform writeonly uimage3D u_Dst;

uint fetchMeTheGoat(in ivec3 coord)
{
    ivec2 sz = imageSize(u_Src).xy;

    if(coord.x >= sz.x || coord.y >= sz.y)
        return 0;
    else
        return imageLoad(u_Src, coord).r;
}

void main()
{
    ivec2 iID = ivec2(gl_GlobalInvocationID.xy);
    ivec2 histoSz = imageSize(u_Dst).xy;

    if(iID.x < histoSz.x && iID.y < histoSz.y) {
        int sz = int(gl_GlobalInvocationID.z) * 32;
        int ez = sz + 32;

        for(int z = sz; z < ez; z++) {
            ivec3 pos = ivec3(iID * 2, z);
            uint val = 0;

            val += fetchMeTheGoat(pos + ivec3(0, 0, 0));
            val += fetchMeTheGoat(pos + ivec3(0, 1, 0));
            val += fetchMeTheGoat(pos + ivec3(1, 0, 0));
            val += fetchMeTheGoat(pos + ivec3(1, 1, 0));

            imageStore(u_Dst, ivec3(iID, z), uvec4(val));
        }
    }
}
